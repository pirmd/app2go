<!--
'         NAME: app2go.wsf
'  DESCRIPTION: app2go script launcher
'      CREATED: 2008.08.08 / REVISION: 2016.05.12 - 16:32
-->
<package>

<job id="launch">

    <resource id="ScriptFolder">config.example\</resource>

    <runtime>
        <unnamed name="script" many="false" required="true"
                 helpstring="Path to the script to execute or script name located in configuration folder" />
        <unnamed name="arg" many="true" required="false"
                 helpstring="Arguments to pass to the script" />
        <named name="clean" type="simple" required="false"
               helpstring="Force cleaning launchers resources (be cautious as it will clean resources even if used by other launchers)" />
        <named name="debug" type="simple" required="false"
               helpstring="Display debug messages" />
        <named name="verbose" type="simple" required="false"
               helpstring="Display verbose messages" />
     </runtime>

    <script language="VBScript" src="./lib2go.vbi"/>
    <script language="VBScript">

       Private Function ScriptFileLocation(Script)
           ScriptByName = ScriptFolder & "\" & Script&".2go"
           If FSO.FileExists(Script) Then  ' Script is a path that exists
                ScriptFileLocation = Script
           ElseIf FSO.FileExists(ScriptByName) Then ' Script is a name to a script configuration
                ScriptFileLocation = ScriptByName
           Else
                Die Script&" configuration file cannot be found"
            End If
        End Function


      '---+ Build folders locations --------------------------------------------
        ScriptFolder = FSO.BuildPath(LauncherFolder, getResource("ScriptFolder"))

      '---+ Get General configuration defintion for app2go ---------------------
        LoadScript ScriptFolder&"\app2go.2go"

      '---+ Parse command line Arguments ---------------------------------------
        ShowWarningMsg()
        For Each arg in WScript.Arguments
            arg = LCase(arg)
            If arg="/verbose" Then
                ShowVerboseMsg()
            ElseIf arg="/debug" Then
                ShowDebugMsg()
            ElseIf arg="/clean" Then
                FORCE_CLEAN = True
            ElseIf App2goScript="" Then
                App2goScript = arg
            Else
                LogDebug "Script argument: "&arg
                ScriptArguments = AddCmdLine(ScriptArguments, SanitizeCmdLineArg(arg))
            End If
        Next
        LogDebug "Command line: " & ScriptArguments
        LogDebug "Timeout : "&WScript.TimeOut
       '---+ Launch the script -------------------------------------------------
        StartLauncher ScriptFileLocation(App2GoScript), ScriptArguments
        WScript.Quit(Err.Number)
    </script>
</job>

</package>
